plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace 'dev.vvasiliev.application'
    compileSdk versions.compileSdk

    defaultConfig {
        applicationId "dev.vvasiliev.application"
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode versions.versionCode
        versionName versions.appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.composeUiVersion
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    flavorDimensions "country", "payment"
    productFlavors{
        BY{
            applicationIdSuffix ".by"
            versionNameSuffix "-by"
            dimension "country"
        }
        EU{
            applicationIdSuffix ".eu"
            versionNameSuffix "-eu"
            dimension "country"
        }
        Paid{
            applicationIdSuffix ".paid"
            versionNameSuffix "-paid"
            dimension "payment"
        }
        Free{
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            dimension "payment"
        }
    }

    //Rename apks
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def versionName = variant.versionName
            outputFileName = "$applicationName-${versionName}.apk"
        }
    }
}

kapt {
    correctErrorTypes = true
}

dependencies {
    implementation libraries.core_ktx
    implementation libraries.lifecycle_runtime_ktx
    implementation libraries.activity_compose
    implementation libraries.compose_ui
    implementation libraries.compose_ui_tooling
    implementation libraries.compose_material
    implementation libraries.composeNavigation
    implementation libraries.daggerHilt
    implementation libraries.hiltNavigation

    kapt libraries.daggerHiltCompiler

    implementation submodules.structures
    implementation submodules.ui
    implementation submodules.audio

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$versions.composeUiVersion"
    debugImplementation "androidx.compose.ui:ui-tooling:$versions.composeUiVersion"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$versions.composeUiVersion"
}